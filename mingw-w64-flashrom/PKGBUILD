# Maintainer: Jonathan Armstrong <jda2158@gmail.com>

_realname=flashrom
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.2
pkgrel=1
pkgdesc="Utility for detecting, reading, writing, verifying and erasing flash chips."
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32')
url="https://github.com/flashrom/flashrom"
license=('gpl-2.0')

depends=("${MINGW_PACKAGE_PREFIX}-libftdi"
             "${MINGW_PACKAGE_PREFIX}-libusb")
makedepends=("${MINGW_PACKAGE_PREFIX}-meson")


source=("https://github.com/flashrom/flashrom/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('a5bac412cefb87bb426912fed46ccc38799d088a9b92dbe7bac38c5df016d9b2')

build() {
  mkdir -p build-${MSYSTEM} && cd build-${MSYSTEM}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
    meson \
      -Dpciutils=false \
      -Dconfig_linux_mtd=false \
      -Dconfig_linux_spi=false \
      --prefix="${MINGW_PREFIX}" \
      --wrap-mode=nodownload \
      --auto-features=enabled \
      --buildtype=plain \
      ../${_realname}-${pkgver}

  meson compile
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  meson test
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"

  # FIXME: do not want to install to mingw64/sbin/ because not on path
  DESTDIR="${pkgdir}" meson install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
 
# echo replace USB-UART drivers (if needed) with WinUSB (libusb) using zadig [https://zadig.akeo.ie/]
}
