_realname=virglrenderer
pkgbase=mingw-w64-${_realname}
pkgname=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgver=1.0.1
pkgrel=1
pkgdesc='A virtual 3D GPU library, that allows the guest operating system to use the host GPU to accelerate 3D rendering'
arch=('any')
mingw_arch=('mingw64' 'ucrt64' 'clang64' 'clangarm64')
url='https://virgil3d.github.io/'
msys2_references=(
  "cpe: cpe:/a:virglrenderer_project:virglrenderer"
)
license=(MIT)
depends=("${MINGW_PACKAGE_PREFIX}-libepoxy")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-python"
  "${MINGW_PACKAGE_PREFIX}-meson"
  "${MINGW_PACKAGE_PREFIX}-pkgconf"
  "${MINGW_PACKAGE_PREFIX}-ninja"
  "${MINGW_PACKAGE_PREFIX}-cc"
  "${MINGW_PACKAGE_PREFIX}-check"
)
source=(
  "https://gitlab.freedesktop.org/virgl/${_realname}/-/archive/${pkgver}/${_realname}-${pkgver}.tar.bz2"
  "0001-Virglrenderer-on-Windows.patch"
)
sha256sums=(
  '53cb8fadd08f5260ee57833fc2488565481438bc7a8e34f3e114d12cc9d9db9a'
  'c09b1fd0025fe376c92b5a61d008a98cc6bb7469d03c81a1bb212767e7cf8866'
)

noextract=("${_realname}-${pkgver}.tar.bz2")

prepare() {
  [[ -d "${srcdir}/${_realname}-${pkgver}" ]] && rm -rf "${srcdir}/$_realname-${pkgver}"
  tar -C "${srcdir}" -xf "${srcdir}/${_realname}-${pkgver}.tar.bz2" || true

  cd "${srcdir}/${_realname}-${pkgver}"
  # Patch inspired by https://github.com/kjliew/qemu-3dfx/tree/master/virgil3d/MINGW-packages
  patch -p2 -i "${srcdir}/0001-Virglrenderer-on-Windows.patch"
}

build() {
  cd "${_realname}-${pkgver}"
  MSYS2_ARG_CONV_EXCL="--prefix" \
  meson \
    --prefix="${MINGW_PREFIX}" \
    --wrap-mode=nodownload \
    -Dtests=true \
    build

  ninja -C build
}

check() {
  cd "${_realname}-${pkgver}"
  ninja -C build test || true
}

package() {
  cd "${_realname}-${pkgver}"
  DESTDIR="$pkgdir" ninja -C build install
  install -D -m644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}/${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
